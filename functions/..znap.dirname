#!/bin/zsh

..znap.dirname.n() {
  emulate -L zsh; setopt extendedglob

  private dir=~znap/$1
  if [[ $1 == */* ]]; then
    dir=~znap/${${1##*/}%.git}
    [[ -d $dir ]] ||
        .znap.clone $1
  fi
  if [[ -d $dir ]]; then
    reply=( $dir )
    return 0
  fi
  return 1
}

..znap.dirname.d() {
  emulate -L zsh; setopt extendedglob

  if [[ $1 == ~znap/* ]]; then
    # private name=${${1#~znap/}:h1}  <- Zsh 5.8+
    private name=${${1#~znap/}%%/*}
    reply=( "$name" ${#:-~znap/$name} )
    return 0
  fi
  return 1
}

..znap.dirname.c() {
  private tag=named-directories
  _tags $tag

  local -a expl=()
  _tags &&
      _requested $tag expl 'named directory' &&
          compadd "$expl[@]" -I ']' -Q - ~znap/*(on-/:t)
}

..znap.dirname() {
  ..znap.dirname.$1 "$2"
}

..znap.dirname "$@"
