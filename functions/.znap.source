#!/bin/zsh
# load plugins
# args: <repo> [ <dir> | <file> ] ...

if ! (( # )); then
  print -u2 'znap source: not enough arguments'
  .znap.help source
  return $(( sysexits[(i)USAGE] + 63 ))
fi

private __file= __src= __repo=~[$1]
shift

for __src in ${@:-''}; do
  __file=~/.local/bin/${__repo:t}${__src:+.}${__src//\//.}
  if ! [[ -r $__file:P ]]; then
    .znap.source.link "$__repo" "$__src" "$__file" ||
        return
  fi

  # Wrap in a named function for profiling purposes.
  .znap.source:${__file:t}() {
    . "$1"
  }
  {
    .znap.source:${__file:t} "$__file:P"
  } always {
    TRY_BLOCK_ERROR=
    unfunction .znap.source:${__file:t}
  }
done
