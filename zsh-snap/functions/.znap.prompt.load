#!/bin/zsh
zmodload -F zsh/parameter p:functions

.znap.prompt.load() {
  emulate -L zsh; setopt $_znap_opts

  private dir=$1 theme=$2
  private setup=prompt_${theme}_setup

  [[ -v functions[$setup] ]] &&
      return

  autoload +X -Uz $setup &>/dev/null &&
      return

  unfunction $setup

  private -a file=( $dir/**/$setup(-.Y1) )
  if [[ -n $file ]]; then

    if autoload +X -Uz $file &>/dev/null; then
      fpath=( $file:h $fpath[@] )
      return
    fi

    unfunction $file
  fi

  file=( $dir/**/*$theme*.{zsh-theme,plugin.zsh,.zsh}(-.Y1) )

  [[ -n $file ]] ||
      return

  eval "$setup() {
    unsetopt localoptions prompt{bang,cr,percent,sp,subst}
    PS1='%# '
    . $file[1]
    private opt=
    for opt in bang cr percent sp subst; do
      [[ -o prompt\$opt ]] &&
          prompt_opts+=( \$opt )
    done
    setopt localoptions
  }"
}

.znap.prompt.load "$@"
