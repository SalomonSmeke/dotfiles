#!/bin/zsh
zmodload zsh/param/private
zmodload -F zsh/zutil b:zparseopts

local -a reply=()
local REPLY=

local -A opts_pre=()
zparseopts -A opts_pre -D -- h -help

if ! (( $# )); then
  .znap.help
  return
fi

private cmd=$1
shift

case $cmd in
  ( cd | ls )
    print -u2 'znap: try `'"$cmd ~${${1:+[$1]}:-znap}"'` instead'
    return $(( sysexits[(i)USAGE] + 63 ))
  ;;
  ( path )
    print -u2 'znap: try `path+=( ~'"${${1:+[$1]}:-znap}"' )` instead'
    return $(( sysexits[(i)USAGE] + 63 ))
  ;;
  ( rm )
    print -u2 'znap: try `rm ~'"${${1:+[$1]}:-znap}"'` instead'
    return $(( sysexits[(i)USAGE] + 63 ))
  ;;
esac

local -A opts_pre=() opts_post=()
zparseopts -A opts_post -D -- h -help

private func=.znap.$cmd
if [[ -v functions[$func] ]]; then
  if (( ${#opts_pre} || ${#opts_post} )); then
    .znap.help $cmd
  else
    $func $@
  fi
else
  print -u2 "znap: unknown command: $cmd"
  .znap.help
  return $(( sysexits[(i)USAGE] + 63 ))
fi
